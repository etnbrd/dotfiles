# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# TODO find a way to securely store online keys, any other non public stuffs.

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi


# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# initialize autocomplete here, otherwise functions won't be loaded
# TODO functions seem to be loaded in fpath.zsh and config.zsh, change that, so that compdef is accessible for all scripts
autoload -U compinit
compinit

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done


# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files